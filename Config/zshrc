# If not running interactively, don't do anything
[[ $- != *i* ]] && return

# ----------------------
# ZSH CORE CONFIGURATION
# ----------------------

# Enable advanced completion system
autoload -Uz compinit && compinit

# Enable colors
autoload -U colors && colors

# Completion menu style
zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"

# ----------------------------
# ENVIRONMENT VARIABLES & PATH
# ----------------------------

export PGDATA="$HOME/postgres_data"
export PGHOST="/tmp"

if [[ -d "$HOME/afs/bin" ]]; then
    path=("$HOME/afs/bin" $path)
fi
if [[ -d "$HOME/.local/bin" ]]; then
    path=("$HOME/.local/bin" $path)
fi

export LANG=en_US.utf8
export NNTPSERVER="news.epita.fr"
export EDITOR=vim

# Ensure unique paths (Zsh specific)
typeset -U path
export PATH

# TAB colors
if [ -x /usr/bin/dircolors ]; then
    eval "$(dircolors -b)"
fi

# Use bat for colored man pages
export MANPAGER="sh -c 'awk '\''{ gsub(/\x1B\[[0-9;]*m/, \"\", \$0); gsub(/.\x08/, \"\", \$0); print }'\'' | bat -p -lman'"

# -----------------------
# HISTORY & SHELL OPTIONS
# -----------------------

HISTFILE=~/afs/.confs/.zsh_history
HISTSIZE=1000
SAVEHIST=2000

# Share history and ignore duplicates
setopt SHARE_HISTORY
setopt HIST_IGNORE_DUPS
setopt HIST_IGNORE_SPACE

# Navigation options
setopt AUTO_CD
setopt CORRECT

# Remap Home and End buttons
bindkey "^[[H" beginning-of-line
bindkey "^[[F" end-of-line

# ------------------------------
# GENERAL ALIASES & REPLACEMENTS
# ------------------------------

# Colorize standard commands
alias diff='diff --color=auto'
alias grep='grep --color=auto'
alias ip='ip -color=auto'
alias cat='bat -pp'
alias cqt='bat -pp'
alias ls='lsd'
alias sl='lsd'
alias la='lsd -A'
alias ll='lsd -l'
alias tree='lsd --tree'

# -----------
# GIT ALIASES
# -----------

alias gs='git status'
alias ga='git add'
alias gc='git commit -m'
alias gp='git push'
alias gpu='git pull'
alias gl='git log --oneline --graph --decorate'
alias gt='git tag -ma'
alias gpt='git push --follow-tags'
alias gd='git diff --name-only --relative --diff-filter=d -z | xargs -0 bat --diff'

# Go to the root of current git repository
cdg() {
    local root_dir
    root_dir=$(git rev-parse --show-toplevel 2>/dev/null)
    if [[ -n "$root_dir" ]]; then
        cd "$root_dir"
    else
        echo "Error : You are not in a Git repository." >&2
        return 1
    fi
}

# ---------------------
# EPITA & C PROGRAMMING
# ---------------------

# Spawns a new terminal in the current directory
alias double='alacritty --working-directory "$PWD" > /dev/null 2>&1 & disown'

# School shortcuts
alias afs='cd ~/afs/'
alias intra="firefox https://intra.forge.epita.fr/"
alias moodle="firefox https://moodle.epita.fr/my/"
alias forge="firefox https://cri.epita.fr/"

# C Dev shortcuts
alias makec="make && make check && make clean"
alias gcw="gcc -std=c99 -pedantic -Werror -Wall -Wextra -Wvla"
alias cf="clang-format -i"

# SQL shortcuts
alias sqlsetup='chmod +x ~/afs/.confs/config/scripts/setup_sql.sh && ~/afs/.confs/config/scripts/setup_sql.sh'
alias sqlserv='postgres -k "$PGHOST"'
alias sqlrun='psql roger_roger -f'

# Apply clang-format to the current repository
cfe() {
    local here=$(pwd)
    cd $(git rev-parse --show-toplevel)
    cp ~/afs/.confs/clang-format ./.clang-format
    clang-format-epita
    rm .clang-format
    cd "$here"
}

# Compile with coverage flags, run lcov, and open report
lc() {
    mkdir -p coverage
    gcc -std=c99 -pedantic -Werror -Wall -Wextra -Wvla "$@" -fPIC -coverage -lgcov -lcriterion
    ./a.out
    lcov --capture --directory ./ --output-file coverage.info --ignore-errors empty --ignore-errors inconsistent
    genhtml coverage.info --output-directory coverage
    rm *.gcno *.gcda coverage.info a.out
    firefox coverage/index.html
}

# -----------------
# UTILITY FUNCTIONS
# -----------------

# Create a directory and enter it
mkcd() {
    mkdir -p "$1" && cd "$1"
}

# Smart archive extractor
extract() {
    for f in "$@"; do
      if [ -f $f ] ; then
        case $f in
              *.tar.bz2)     tar xvjf $f    ;;
              *.tar.gz)      tar xvzf $f    ;;
              *.bz2)         bunzip2 $f     ;;
              *.gz)          gunzip $f      ;;
              *.tar)         tar xvf $f     ;;
              *.tbz2)        tar xvjf $f    ;;
              *.zip)         unzip $f       ;;
              *.Z)           uncompress $f  ;;
              *)             echo "Don't know how to extract '$f'..."
          esac
        else
          echo "'$f' is not a valid file!"
        fi
    done
}

extpls() {
    if [ -n "$ZSH_VERSION" ]; then
        setopt local_options no_nomatch
    fi

    mv ~/Downloads/*.{tar,bz2,gz,tbz2,zip,Z,rar,7z} . 2> /dev/null

    for ext in tar bz2 gz tbz2 zip Z rar 7z; do
        for file in *.$ext; do
            if [ -f "$file" ]; then
                extract "$file"
                rm "$file"
            fi
        done
    done
}

# Clipboard utilities (requires xsel)
copy() {
    cat "$@" | xsel -b
}

copyname() {
    (
        for file in "$@"; do
            if [ -f "$file" ]; then
                echo "$file"
                cat "$file"
                echo
            fi
        done
    ) | xsel -b
}

# ------
# CONFIG
# ------

update-conf() {
    setopt local_options no_monitor
    echo "=================================================="
    echo "\b\033[0;31m/!\Please don't close the terminal during update/!\\033[0m";
    echo -ne "\033[0;34m::\033[0m Updating Epidots... "
    local err=$(mktemp)
    (
        set -e
        tmp=$(mktemp -d)
        git clone --depth 1 https://github.com/tsunooky/epidots.git "$tmp"
        cp -r "$tmp/Config/"* "$HOME/afs/.confs/"
        AFS_DIR="$HOME/afs" "$HOME/afs/.confs/install.sh"
        rm -rf "$tmp"
    ) >"$err" 2>&1 &
    local pid=$! sp='/-\|' i=0
    while kill -0 $pid 2>/dev/null; do printf "\b${sp[(i++%4)+1]}"; sleep 0.1; done
    wait $pid
    if [ $? -eq 0 ]; then
        rm "$err"
        echo -e "\b\033[0;32m[OK]\033[0m"
        i3-msg restart >/dev/null 2>&1
        source "${ZDOTDIR:-$HOME}/.zshrc"
    else
        echo -e "\b\033[0;31m[FAIL]\033[0m"; cat "$err"; rm "$err"
    fi
}

reset-conf() {
    echo -e "\n\033[0;31m=== FACTORY RESET ===\033[0m"
    echo -ne "\033[0;34m::\033[0m Restoring defaults...   "
    if curl -L https://raw.githubusercontent.com/Darkrentin/default-nixos-config-epita/refs/heads/main/installer.sh 2>/dev/null | sh -s >/dev/null 2>&1; then
        echo -e "\033[0;32m[OK]\033[0m"
    else
        echo -e "\033[0;31m[FAIL]\033[0m"; return 1
    fi
    echo -ne "\033[0;34m::\033[0m Cleaning files...       "
    local i3c="$HOME/afs/.confs/config/i3/config"
    [ -f "$i3c" ] && cp "$i3c" "$HOME/config.temp"
    rm -rf "$HOME/afs/.confs/"{wallpapers,config/{alacritty,matugen,gtk-*,qt*ct,rofi,scripts,polybar,picom,starship.toml}}
    [ -f "$HOME/config.temp" ] && mkdir -p "$(dirname "$i3c")" && mv "$HOME/config.temp" "$i3c"
    echo -e "\033[0;32m[OK]\033[0m"
    pkill polybar; pkill picom; i3-msg restart >/dev/null 2>&1
    "$HOME/afs/.confs/install.sh" >/dev/null 2>&1
    echo -e "\033[0;32mReset done.\033[0m Restart terminal."
}

# Switch between WIN and ALT mod key
alias winpls="sed -i 's/set \$mod Mod1/set \$mod Mod4/' ~/.config/i3/config && i3-msg restart > /dev/null && echo 'Mod changed to WIN'"
alias altpls="sed -i 's/set \$mod Mod4/set \$mod Mod1/' ~/.config/i3/config && i3-msg restart > /dev/null && echo 'Mod changed to ALT'"

# Wallpaper & Matugen scripts
alias bg='~/afs/.confs/config/scripts/change_wallpaper.sh'
alias rbg='(~/afs/.confs/config/scripts/random_wallpaper.sh &)'
alias bgdir='cd ~/afs/.confs/wallpapers'
bgadd() {
    cp "$1" ~/afs/.confs/wallpapers
}

# --- Custom aliases ---
alias tarpls='extpls'
alias clip='copy'
alias repo='cdg'
alias doom='wget "http://doomfactory.free.fr/NoHotLink/Doom.rar" -O ~/.doom.rar && nix-shell -p unrar --command "unrar x ~/.doom.rar <<< Y" && mv ~/Doom.wad ~/.doom.wad && nix-shell -p doomretro --command "doomretro -iwad ~/.doom.wad"'
alias err='echo $?'

if [ -d "afs" ]; then
    cd ~/afs
fi

# ----- Prompt init with starship -----
eval "$(starship init zsh)"

# -----------------------------
# ZSH PLUGINS (Arch Linux path)
# -----------------------------

if [ -f "$HOME/.nix-profile/share/zsh-autosuggestions/zsh-autosuggestions.zsh" ]; then
    source "$HOME/.nix-profile/share/zsh-autosuggestions/zsh-autosuggestions.zsh"
    ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=60'
fi

# Syntax highlighting (DOIT toujours être à la fin du fichier)
if [ -f "$HOME/.nix-profile/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh" ]; then
    source "$HOME/.nix-profile/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
fi
